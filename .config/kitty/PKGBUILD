# Patched kitty terminal
# The patch removes a refresh on new keyboard connect. The Anne Pro keeps
# spamming new keyboard connected events.

pkgname=kitty-annepro-patch
pkgver=0.18.1.r4.gb71b3442
pkgrel=1
pkgdesc="A modern, hackable, featureful, OpenGL based terminal emulator"
arch=(i686 x86_64)
url="https://github.com/kovidgoyal/kitty"
license=(GPL3)
depends=(python3 freetype2 fontconfig wayland libx11 libgl libcanberra)
makedepends=(git python-setuptools libxinerama libxcursor libxrandr libxkbcommon libxkbcommon-x11 glfw-x11 wayland-protocols mesa python-sphinx)
optdepends=('imagemagick: viewing images with icat')
provides=(kitty)
conflicts=(kitty kitty-terminfo)
source=("git+https://github.com/Linouth/kitty.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/kitty"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/kitty"
  python3 setup.py linux-package --prefix "${pkgdir}"/usr
}
